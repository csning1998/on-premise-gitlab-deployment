terraform {
  required_providers {
    null = {
      source  = "hashicorp/null"
      version = "3.2.2"
    }
  }
}

/*
* Generate the Ansible inventory file from template
*/
resource "local_file" "inventory" {
  content = templatefile("${path.root}/../../templates/inventory-kubeadm-cluster.yaml.tftpl", {
    master_nodes     = [for node in var.inventory.nodes : node if startswith(node.key, "kubeadm-master")],
    worker_nodes     = [for node in var.inventory.nodes : node if startswith(node.key, "kubeadm-worker")],
    ansible_ssh_user = var.vm_credentials.username,
    extra_vars       = var.ansible_config.extra_vars
    registry_host    = var.ansible_config.registry_host
  })
  filename        = "${var.ansible_config.root_path}/inventory-kubeadm-cluster.yaml"
  file_permission = "0644"
}

resource "null_resource" "provision_cluster" {

  depends_on = [local_file.inventory]

  provisioner "local-exec" {

    working_dir = abspath("${path.root}/../../../")

    /*
     * To avoid "(output suppressed due to sensitive value in config)" shown in terminal
     * Use `nonsensitive()` function to decrypt the playbook log.
     * `rm-rf` is to ensure the destination directory for fetched files exists and is clean
    */
    command = <<-EOT
      set -e
      rm -f ${var.ansible_config.root_path}/fetched/kubeconfig
      mkdir -p ${var.ansible_config.root_path}/fetched

      ansible-playbook \
        -i ${local_file.inventory.filename} \
        --private-key ${nonsensitive(var.vm_credentials.ssh_private_key_path)} \
        --ssh-common-args='-F ${var.ansible_config.ssh_config_path}' \
        --extra-vars "ansible_ssh_user=${nonsensitive(var.vm_credentials.username)}" \
        -v \
        ${var.ansible_config.root_path}/playbooks/10-provision-kubeadm.yaml
    EOT

    interpreter = ["/bin/bash", "-c"]
  }
}


data "external" "fetched_kubeconfig" {

  depends_on = [null_resource.provision_cluster]
  program = ["/bin/bash", "-c", <<-EOT
    set -e
    KUBECONFIG_PATH="${var.ansible_config.root_path}/fetched/kubeconfig"
    if [ ! -f "$KUBECONFIG_PATH" ]; then
      echo '{}'
      exit 0
    fi

    jq -n --arg kc "$(cat $KUBECONFIG_PATH)" '{"content": $kc}'
  EOT
  ]
}
