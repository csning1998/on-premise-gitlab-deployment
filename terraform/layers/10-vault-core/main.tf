module "provisioner_kvm" {
  source = "../../modules/81-provisioner-kvm"

  # --- Map Layer's specific variables to the Module's generic inputs ---

  # VM Configuration
  vm_config = {
    all_nodes_map   = local.all_nodes_map
    base_image_path = var.vault_cluster_config.base_image_path
  }

  # VM Credentials from Vault
  credentials = {
    username            = data.vault_generic_secret.iac_vars.data["vm_username"]
    password            = data.vault_generic_secret.iac_vars.data["vm_password"]
    ssh_public_key_path = data.vault_generic_secret.iac_vars.data["ssh_public_key_path"]
  }

  # Libvirt Network & Storage Configuration
  libvirt_infrastructure = {
    network = {
      nat = {
        name_network = var.vault_infrastructure.network.nat.name_network
        name_bridge  = var.vault_infrastructure.network.nat.name_bridge
        mode         = "nat"
        ips          = var.vault_infrastructure.network.nat.ips
      }
      hostonly = {
        name_network = var.vault_infrastructure.network.hostonly.name_network
        name_bridge  = var.vault_infrastructure.network.hostonly.name_bridge
        mode         = "route"
        ips          = var.vault_infrastructure.network.hostonly.ips
      }
    }
    storage_pool_name = var.vault_infrastructure.storage_pool_name
  }
}

module "ssh_config_manager_vault" {
  source = "../../modules/82-ssh-config-manager"

  config_name = var.vault_cluster_config.cluster_name
  nodes       = module.provisioner_kvm.all_nodes_map
  vm_credentials = {
    username             = data.vault_generic_secret.iac_vars.data["vm_username"]
    ssh_private_key_path = data.vault_generic_secret.iac_vars.data["ssh_private_key_path"]
  }
  status_trigger = module.provisioner_kvm.vm_status_trigger
}

module "bootstrapper_ansible_cluster" {
  source = "../../modules/83-bootstrapper-ansible-generic"

  ansible_config = {
    root_path       = local.ansible_root_path
    ssh_config_path = module.ssh_config_manager_vault.ssh_config_file_path
    playbook_file   = "playbooks/10-provision-vault.yaml"
    inventory_file  = "inventory-vault-cluster.yaml"
  }

  inventory_content = templatefile("${path.root}/../../templates/inventory-vault-cluster.yaml.tftpl", {
    ansible_ssh_user        = data.vault_generic_secret.iac_vars.data["vm_username"]
    haproxy_node            = local.haproxy_node
    vault_nodes             = local.vault_nodes_map
    vault_nat_subnet_prefix = local.vault_nat_network_subnet_prefix
    vault_ha_virtual_ip     = var.vault_cluster_config.ha_virtual_ip
    vault_allowed_subnet    = var.vault_infrastructure.vault_allowed_subnet
  })

  vm_credentials = {
    username             = data.vault_generic_secret.iac_vars.data["vm_username"]
    ssh_private_key_path = data.vault_generic_secret.iac_vars.data["ssh_private_key_path"]
  }

  extra_vars = {
    "vault_keepalived_auth_pass" = data.vault_generic_secret.db_vars.data["vault_keepalived_auth_pass"]
    "vault_haproxy_stats_pass"   = data.vault_generic_secret.db_vars.data["vault_haproxy_stats_pass"]
  }

  status_trigger = module.ssh_config_manager_vault.ssh_access_ready_trigger
}
